/*
 * The MIT License (MIT)
 *
 * Copyright (c) 2019-2023 Patrick Reinhart
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */
plugins {
  id 'eclipse'
  id 'java-library'
  id 'com.bmuschko.izpack' version '3.2'
  id 'org.kordamp.gradle.java-project' version '0.54.0'
}

ext {
  installerRes = file("$buildDir/installer.res")
}

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
}

repositories {
  mavenLocal()
  maven {
    url 'https://oss.sonatype.org/content/repositories/snapshots'
  }
  mavenCentral()
}

sourceSets.main.resources {
  exclude "**/izpack/*"
}

configurations {
  izpack
  installer.extendsFrom implementation
}

dependencies {
  izpack 'org.codehaus.izpack:izpack-ant:5.2.3-SNAPSHOT'

  implementation 'org.codehaus.izpack:izpack-api:5.2.3-SNAPSHOT'
  implementation 'org.codehaus.izpack:izpack-core:5.2.3-SNAPSHOT'
  implementation 'org.codehaus.izpack:izpack-panel:5.2.3-SNAPSHOT'

  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.0'
  testImplementation 'org.junit.jupiter:junit-jupiter-migrationsupport:5.11.0'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.3'
  testRuntimeOnly 'org.junit.platform:junit-platform-runner:1.11.0'
  testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.10.3'
}

test {
  useJUnitPlatform()
  jvmArgs '-Duser.language=de', '-Duser.country=CH', '-Duser.timezone=Europe/Berlin', '-XX:-TieredCompilation'
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'pr', 'ea'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-+]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

izpack {
  baseDir = file("src/main/izpack")
  compression = 'deflate'
  compressionLevel = 9
  appProperties = ['copyright.holder': 'Patrick Reinhart',
                   'copyright.email': 'patrick@reini.net',
                   'copyright.year': '2019',
                   'product.name': 'izPack Demo Product', 
                   'product.version' : version,
                   'product.url' : 'http://www.reini.net',
                   'product.subpath': 'izPackDemoProduct',
                   'install.res': installerRes,
                   'install.jar': file("$buildDir/libs/izPackDemoInstaller-${version}.jar")]
}

izPackCreateInstaller.dependsOn  = ['jar']

defaultTasks 'izPackCreateInstaller'

eclipse.classpath.downloadSources = true

config {
    release = (rootProject.findProperty('release') ?: false).toBoolean()
    docs {
        javadoc {
            autoLinks {
                excludes = [ 
                    'izpack-api-5.2.0-SNAPSHOT',
                    'izpack-core-5.2.0-SNAPSHOT',
                    'izpack-panel-5.2.0-SNAPSHOT'
                ]
            }
        }
    }
    info {
        vendor = 'reinhapa'
        name = 'izPackDemoInstaller'
        description = 'Sample izPack based installer'
        inceptionYear = '2019'
        links {
            website = 'https://github.com/reinhapa/izPackDemoInstaller'
            issueTracker = 'https://github.com/reinhapa/izPackDemoInstaller/issues'
            scm = 'https://github.com/reinhapa/izPackDemoInstaller.git'
        }
        scm {
            connection = 'scm:git://github.com/reinhapa/izPackDemoInstaller.git'
            developerConnection = 'scm:git://github.com/reinhapa/izPackDemoInstaller.git'
            url = 'https://github.com/reinhapa/izPackDemoInstaller'
        }
        people {
            person {
                id = 'reinhapa'
                name = 'Patrick Reinhart'
                email = 'patrick@reini.net'
                roles = [ 'developer', 'maintainer' ]
            }
        }
    }
    licensing {
        licenses {
            license {
                id = 'MIT'
            }
        }
    }
}

normalization {
    runtimeClasspath {
        ignore('/META-INF/MANIFEST.MF')
    }
}
