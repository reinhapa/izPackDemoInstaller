/*
 * The MIT License (MIT)
 *
 * Copyright (c) 2019-2020 Patrick Reinhart
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */
plugins {
  id 'java-library'
  id 'com.bmuschko.izpack' version '3.0'
  id 'org.kordamp.gradle.java-project' version '0.38.0'
}

ext {
  installerRes = file("$buildDir/installer.res")
}

repositories {
  mavenLocal()
  mavenCentral()
}

sourceSets.main.resources {
  exclude "**/izpack/*"
}

configurations {
  izpack
  installer
}

dependencies {
  izpack "org.codehaus.izpack:izpack-ant:${rev_izpack}"

  implementation "org.codehaus.izpack:izpack-api:${rev_izpack}"
  implementation "org.codehaus.izpack:izpack-core:${rev_izpack}"
  implementation "org.codehaus.izpack:izpack-panel:${rev_izpack}"

  testImplementation "org.junit.jupiter:junit-jupiter-api:${rev_junit_jupiter}"
  testImplementation "org.junit.jupiter:junit-jupiter-migrationsupport:${rev_junit_jupiter}"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${rev_junit_jupiter}"
  testRuntimeOnly "org.junit.platform:junit-platform-runner:${rev_junit_platform}"
  testRuntimeOnly "org.junit.vintage:junit-vintage-engine:${rev_junit_jupiter}"

  installer configurations.compile
}

test {
  useJUnitPlatform()
  jvmArgs '-Duser.language=de', '-Duser.country=CH', '-Duser.timezone=Europe/Berlin', '-XX:-TieredCompilation'
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'pr', 'ea'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-+]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

izpack {
  baseDir = file("src/main/izpack")
  compression = 'deflate'
  compressionLevel = 9
  appProperties = ['copyright.holder': 'Patrick Reinhart',
                   'copyright.email': 'patrick@reini.net',
                   'copyright.year': '2019',
                   'product.name': 'izPack Demo Product', 
                   'product.version' : version,
                   'product.url' : 'http://www.reini.net',
                   'product.subpath': 'izPackDemoProduct',
                   'install.res': installerRes,
                   'install.jar': file("$buildDir/libs/izPackDemoInstaller-${version}.jar")]
}

izPackCreateInstaller.dependsOn  = ['jar']

defaultTasks 'izPackCreateInstaller'

config {
    release = (rootProject.findProperty('release') ?: false).toBoolean()

    info {
        vendor = 'reinhapa'
        name = 'izPackDemoInstaller'
        description = 'Sample izPack based installer'
        inceptionYear = '2019'
        links {
            website = 'https://github.com/reinhapa/izPackDemoInstaller'
            issueTracker = 'https://github.com/reinhapa/izPackDemoInstaller/issues'
            scm = 'https://github.com/reinhapa/izPackDemoInstaller.git'
        }
        scm {
            connection = 'scm:git://github.com/reinhapa/izPackDemoInstaller.git'
            developerConnection = 'scm:git://github.com/reinhapa/izPackDemoInstaller.git'
            url = 'https://github.com/reinhapa/izPackDemoInstaller'
        }
        people {
            person {
                id = 'reinhapa'
                name = 'Patrick Reinhart'
                email = 'patrick@reini.net'
                roles = [ 'developer', 'maintainer' ]
            }
        }
    }
    licensing {
        licenses {
            license {
                id = 'MIT'
            }
        }
    }
}

normalization {
    runtimeClasspath {
        ignore('/META-INF/MANIFEST.MF')
    }
}

